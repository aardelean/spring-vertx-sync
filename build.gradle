plugins {
    id 'me.champeau.gradle.jmh' version '0.2.0'
}
group 'home'
version '1.0-SNAPSHOT'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'me.champeau.gradle.jmh'


sourceCompatibility = '1.8'
mainClassName='home.spring.vertx.sync.Starter'
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "http://repo.spring.io/libs-snapshot" }
}
configurations {
    jmh
    provided
    quasar
    capsule
}
jmh {
    jmhVersion = '1.3.2'
}
sourceSets {
    main {
        compileClasspath += configurations.provided + configurations.jmh
        runtimeClasspath += configurations.provided + configurations.jmh
    }
    test {
        compileClasspath += configurations.provided + configurations.jmh
        runtimeClasspath += configurations.provided + configurations.jmh
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter:1.2.6.RELEASE"
    compile ("org.springframework.boot:spring-boot-starter-data-jpa:1.2.6.RELEASE"){
        exclude group: "org.hibernate"
    }
    compile "org.hibernate:hibernate-core:5.0.1.Final"
    compile "org.hibernate:hibernate-entitymanager:5.0.1.Final"
    compile "mysql:mysql-connector-java:5.1.36"
    compile "org.infinispan:infinispan-spring4:8.0.1.Final"
    compile "org.infinispan:infinispan-cachestore-jpa:8.0.1.Final"
    compile ("org.hibernate:hibernate-infinispan:5.0.1.Final"){
        exclude module: "infinispan-core"
    }
    compile "org.projectlombok:lombok:1.16.4"
    compile "org.freemarker:freemarker:2.3.22"
    compile "io.vertx:vertx-core:3.1.0"
    compile "io.vertx:vertx-mongo-client:3.1.0"
    compile "io.vertx:vertx-web:3.1.0"
    compile "io.vertx:vertx-sync:3.1.0"
    compile "io.advantageous.boon:boon-json:0.5.5"
    compile "co.paralleluniverse:quasar-core:0.7.3:jdk8"
    compile "org.glassfish.jersey.core:jersey-client:2.22"
    // quasar instrumentation
    quasar "co.paralleluniverse:quasar-core:0.7.3:jdk8"
    //building
    capsule "co.paralleluniverse:capsule:1.0.1"
    //testing
    testCompile 'junit:junit:4.11'
    //benchmarking
    jmh 'commons-io:commons-io:2.4'
    provided 'org.openjdk.jmh:jmh-core:1.5.1'
    provided 'org.openjdk.jmh:jmh-generator-annprocess:1.5.1'
}

run {
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}  " +
//            "-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000 " +
            "-Xms2G -Xmx2G -server " +
            "-XX:+UseNUMA " +
            "-XX:+UseParallelGC " +
            "-XX:+AggressiveOpts " +
            "-Dvertx.disableWebsockets=true " +
            "-Dvertx.flashPolicyHandler=false " +
            "-Dvertx.threadChecks=false " +
            "-Dvertx.disableContextTimings=true " +
            "-Dvertx.disableTCCL=true"
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "vertx-capsule.jar"
    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    manifest {
        attributes(
                'Premain-Class'  :   'Capsule',
                'Main-Class'  :   'Capsule',
                'Application-Class'   : mainClassName,
                'Extract-Capsule' : 'false', // no need to extract the capsule
                'Min-Java-Version' : '1.8.0',
                'JVM-Args' : run.jvmArgs.join(' '),
                'System-Properties' : run.systemProperties.collect { k,v -> "$k=$v" }.join(' '),
                'Java-Agents' : getDependencies(configurations.quasar).iterator().next(),
                'Dependencies': getDependencies(configurations.runtime).join(' ')
        )
    }
}

// converts Gradle dependencies to Capsule dependencies
def getDependencies(config) {
    return config.getAllDependencies().collect {
        def res = it.group + ':' + it.name + ':' + it.version +
                (!it.artifacts.isEmpty() ? ':' + it.artifacts.iterator().next().classifier : '')
        if(!it.excludeRules.isEmpty()) {
            res += "(" + it.excludeRules.collect { it.group + ':' + it.module }.join(',') + ")"
        }
        return res
    }
}